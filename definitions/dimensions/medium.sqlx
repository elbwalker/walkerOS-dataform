config {
    type: "view",
    name: "dimension-medium",
    description: "Calculates a system-defined medium",
}

WITH
  dimension_source AS (
  SELECT
    *
  FROM
    ${ref("dimension-source")} ),
  dss AS (
  SELECT
    *
  FROM
    ${ref("data-session_start")} )
SELECT
  dss.timestamp,
  dss.id,
  dss.referrer,
  dss.marketing,
  s.category,
  CASE
  -- UTM
    WHEN dss.medium IS NOT NULL THEN dss.medium
  -- Direct traffic
    WHEN (dss.referrer IS NULL
    AND dss.marketing IS NULL) THEN 'direct'
  -- Organic Search traffic
    WHEN ((s.category = 'search' OR s.category = 'social') AND dss.marketing IS NULL) THEN 'organic'
  -- Paid
    WHEN (dss.clickId IS NOT NULL
    OR REGEXP_CONTAINS(dss.medium, r'^(.*cp.*|ppc|paid.*)$')) THEN 'paid'
    ELSE 'other'
END
  AS medium
FROM
  dss
JOIN
  dimension_source s
ON
  dss.id = s.id
  AND dss.timestamp = s.timestamp
WHERE
  1=1
  --AND DATE(dss.timestamp) >= DATE_SUB(CURRENT_DATE(), INTERVAL 2 DAY)
